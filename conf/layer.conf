# We have a conf and classes directory, add to BBPATH
BBPATH .= ":${LAYERDIR}"

# We have recipes-* directories, add to BBFILES
BBFILES += "${LAYERDIR}/recipes-*/*/*.bb ${LAYERDIR}/recipes-*/*/*.bbappend"

BBFILE_COLLECTIONS += "homeassistant"
BBFILE_PATTERN_homeassistant = "^${LAYERDIR}/"
BBFILE_PRIORITY_homeassistant = "10"

# Most dependencies come from the meta-python layer
LAYERDEPENDS_homeassistant = "\
    core \
    openembedded-layer \
    meta-python \
    networking-layer \
"

LAYERSERIES_COMPAT_homeassistant = "styhead walnascar whinlatter"


# The dynamic-layers directory hosts the extensions and layer specific
# modifications.
#
# The .bbappend and .bb files are included if the respective layer
# collection is available.
BBFILES += "${@' '.join('${LAYERDIR}/dynamic-layers/%s/recipes*/*/*.bbappend' % layer \
               for layer in BBFILE_COLLECTIONS.split())}"
BBFILES += "${@' '.join('${LAYERDIR}/dynamic-layers/%s/recipes*/*/*.bb' % layer \
               for layer in BBFILE_COLLECTIONS.split())}"

BBFILES_DYNAMIC += " \
    swupdate:${LAYERDIR}/dynamic-layers/swupdate/*/*/*.bb \
    swupdate:${LAYERDIR}/dynamic-layers/swupdate/*/*/*.bbappend \
"

# Recommended dependencies for dynamic-layers
LAYERRECOMMENDS_homeassistant = "\
    swupdate \
"

# Meta-oe has a newer version of python3-gspread but this breaks homeassistant
PREFERRED_VERSION_python3-gspread = "5.5.0"
